#!/usr/bin/env perl

use strict;
use warnings;
use Pod::Usage qw/pod2usage/;
use Getopt::Long;
use FindBin qw/$RealBin/;
use lib "$RealBin/../lib";
use Utils;

my ($inslst, 	#insert list
    $dellst, 	#delete list
    $db,   	#db 
    $filelst,	#filelist
    $hg18,	#use hg18 if true
    $annodir,
    $listfile,
    $bincol,  	#first column is BIN, used for direct downloading from UCSC
);

@ARGV or pod2usage();
GetOptions(
    "inslist=s"		=>	\$listfile,
    "insert|i=s"	=>	\$inslst,
    "delete|d=s"	=>	\$dellst,
    "file|f=s"		=>	\$filelst,
    "annodir=s"		=>	\$annodir,
    "hg18"		=>	\$hg18,
    "bincol"		=>	\$bincol,
) or pod2usage ();

#insert BED tables from a specific db
#delete tables from a specific db
my $executedb="$RealBin/../src/executeDB.py";
my $annovar="annotate_variation.pl";
my $liftover="liftOver";
my $hg19to18chain="hg19ToHg18.over.chain";
my %chr2no=(
    'chr1' => '1',
    'chr2' => '2',
    'chr3' => '3',
    'chr4' => '4',
    'chr5' => '5',
    'chr6' => '6',
    'chr7' => '7',
    'chr8' => '8',
    'chr9' => '9',
    'chr10' => '10',
    'chr11' => '11',
    'chr12' => '12',
    'chr13' => '13',
    'chr14' => '14',
    'chr15' => '15',
    'chr16' => '16',
    'chr17' => '17',
    'chr18' => '18',
    'chr19' => '19',
    'chr20' => '20',
    'chr21' => '21',
    'chr22' => '22',
    'chrx' => '23',
    'chry' => '24',
    'chrmito' => '25',
    'chrxy' => '26',
);

$db=shift @ARGV;
warn "No database\n" and pod2usage() unless $db;
my @ins=split /,/,$inslst if $inslst;
my @del=split /,/,$dellst if $dellst;
my @file=split /,/,$filelst if $filelst;
my @unlink;

warn ("NOTICE: Both plot database and annotation database will be updated.\n");
if ($listfile)
{
    warn "NOTICE: use tables in $listfile.\n";
    warn "$inslst and $filelst will be ignored.\n" if ($inslst || $filelst);
    my %tmp=&Utils::readObj($listfile);
    @ins=map { $tmp{$_}{table} } keys %tmp;
    @file=();
} 

#first process deletion
if (@del)
{
    for my $del_table(@del)
    {
	#delete table in locuszoom database
	if (!system("$executedb --db $db --delete $del_table"))
	{
	    warn "$del_table deleted.\n";
	} else
	{
	    warn "Cannot delete $del_table from $db: $!\n";
	    warn "Remaining tables:\n";
	    !system("sqlite3 $db .table") or die "Failed to show tables: $!\n";
	    die "\n";
	}
	#delete ANNOVAR tables
	if (defined $annodir)
	{
	    my $annovar_db=($hg18? "hg18": "hg19")."_$del_table.txt";
	    $annovar_db=File::Spec->catfile($annodir,$annovar_db);
	    if (-f $annovar_db)
	    {
		unlink $annovar_db and warn "$annovar_db deleted\n";
	    }
	}
    }
}

#then process insertion

if (@ins)
{
    ##resolve table file for each table name
    @file=&filecheck(\@ins,\@file) or die "ERROR: not every insertion table corresponds to a file\n";
    for my $i (0..$#file)
    {
	my $name=$ins[$i];
	my $file=$file[$i];
	! system("$executedb --db $db --tablefile $file --tablename $name")
	    or die "Cannot insert $name ($file) into $db\n";
	#if ($annodir)
	#{
	#my $avfile=($hg18? "hg18":"hg19")."_$name.txt";
	#$avfile=File::Spec->catfile($annodir,$avfile);
	#! system("cp $file $avfile") or die "Failed to copy $name($file) to $annodir: $!\n";
	#}
    }
}

#cleanup
unlink @unlink or die "Failed to clean @unlink: $!\n";

##########################SUBROUTINES########################
sub bed2generic
{
    my @generic_tables;
    for my $table (@_)
    {
	open IN, "<", $table or die "Cannot open $table\n";
	my @output;
	while (<IN>)
	{
	    s/[\r\n]+$//;
	    next if /^(track|#|browser|\s)/i; #skip header
	    my @line=split (/\t/,$_);
	    if ($bincol)
	    {
		shift @line; #remove BIN column
	    }
	    next unless @line>=5; 
	    $line[0]=lc($line[0]);
	    next unless $chr2no{$line[0]};
	    push @output, join("\t",$chr2no{$line[0]},$line[1],$line[2],$line[4]),"\n";
	}
	close IN;
	my $outfile="/tmp/annodb_admin.$$.generic.".rand($$);
	open OUT, ">", $outfile or die "Cannot create $outfile\n";
	print OUT join("\t",'chr','start','end','score'),"\n";
	print OUT @output;
	close OUT;
	push @unlink,$outfile;
	push @generic_tables,$outfile;
    }
    return wantarray? @generic_tables:$generic_tables[0];
}

sub filecheck
{

    my @names=@{shift @_};
    my @tables=@{shift @_};
    if (@names == @tables)
    {
	return @tables;
    } elsif (@tables == 0)
    {
	warn ("Try to download tables from UCSC genome browser\n");
	for my $item (@names)
	{
	    my $url="hgdownload.cse.ucsc.edu/goldenPath/".($hg18? "hg18":"hg19")."/database/$item.txt.gz";
	    my $file="$item.txt.gz";
	    my $unpack_file="$item.txt";
	    push @unlink,$file;
	    push @unlink,$unpack_file;
	    if (&Utils::getstore($url,$file))
	    {
		warn "Unpacking ...\n";
		&Utils::extract_archive($file,$unpack_file) or die "Cannot unpack $file\n";
	    } else
	    {
		if ($hg18)
		{
		    my $tmp="/tmp/$item.hg19.txt.gz"; 
		    push @unlink,$tmp;
		    my $tmp_unpack="/tmp/$item.hg19.txt"; 
		    push @unlink,$tmp_unpack;
		    my $tmp_nohead="/tmp/$item.hg19.nohead.txt";
		    push @unlink,$tmp_nohead;
		    my $tmp_nobin="/tmp/$item.hg19.nobin.txt";
		    push @unlink,$tmp_nobin;

		    warn "Cannot download $item from UCSC\n";
		    warn "Try hg19 version\n";
		    $url=~s%/hg18/%/hg19/%;
		    &Utils::getstore($url,$tmp) or die "Cannot get $item\n";
		    &Utils::extract_archive($tmp,$tmp_unpack) or die "Cannot unpack $tmp\n";
		    &rmheader($tmp_unpack,$tmp_nohead);
		    ! system("$liftover -bedPlus=3 $tmp_nohead $hg19to18chain $tmp_nobin /tmp/$$.iii.tmp ") or die "Failed to liftover $item from hg19 to hg18: $!\n";
		    &addbin($tmp_nobin,$unpack_file);
		} else
		{
		    die "Cannot download $file from UCSC\n";
		}
	    }
	    warn "Extracting chr,start,end,score ...\n";
	    my ($convert_file)=&bed2generic($unpack_file);
	    push @tables,$convert_file;
	    if (defined $annodir)
	    {
		my $annovar_db=($hg18? "hg18":"hg19")."_$unpack_file";
		$annovar_db=File::Spec->catfile($annodir,$annovar_db);
		!system("perl -ne 'next if /^(track|#|browser|\\s)/i; print' $unpack_file > $annovar_db") or die "Failed to remove $unpack_file header: $!\n";
		warn "ANNOVAR database: $annovar_db done.\n";
	    }
	}

    } else
    {
	return 0;
    }
    return @tables;
}
sub rmheader
{
    my $in=shift;
    my $out=shift;

    open IN,'<',$in or die "Can't read $in: $!\n";
    open OUT,'>',$out or die "Can't write to $out: $!\n";
    while (<IN>)
    {
	    next if /^(track|#|browser|\s)/i; #skip header
	    s/^\S+\s//; #remove first column (bin col)
	    print OUT $_;
    }
    close IN;
    close OUT;
}
sub addbin
{
    my $in=shift;
    my $out=shift;

    open IN,'<',$in or die "Can't read $in: $!\n";
    open OUT,'>',$out or die "Can't write to $out: $!\n";
    while (<IN>)
    {
	    s/^/1\t/; #remove first column (bin col)
	    print OUT $_;
    }
    close IN;
    close OUT;
}


__END__


=head1 NAME

annodb_admin

=head1 SYNOPSIS

annodb_admin [options] <database>

 -inslist	a file storing tables and associated properties
 -insert,-i	list of table names to be inserted
 -delete,-d	list of table names to be deleted
 -file,-f	list of table files to be inserted
 -annodir	annovar database folder
 -hg18		use it if build version is hg18
 -bincol	first column in BED is BIN column

 NOTE: All list must be comma-separated.
 NOTE: Each table file, if supplied, must has a corresponding table name.
 NOTE: If no table file supplied, it will be downloaded from UCSC server.

=cut
